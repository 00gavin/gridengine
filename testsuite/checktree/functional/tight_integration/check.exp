#!/vol2/TCL_TK/glinux/bin/expect
#___INFO__MARK_BEGIN__
##########################################################################
#
#  The Contents of this file are made available subject to the terms of
#  the Sun Industry Standards Source License Version 1.2
#
#  Sun Microsystems Inc., March, 2001
#
#
#  Sun Industry Standards Source License Version 1.2
#  =================================================
#  The contents of this file are subject to the Sun Industry Standards
#  Source License Version 1.2 (the "License"); You may not use this file
#  except in compliance with the License. You may obtain a copy of the
#  License at http://gridengine.sunsource.net/Gridengine_SISSL_license.html
#
#  Software provided under this License is provided on an "AS IS" basis,
#  WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING,
#  WITHOUT LIMITATION, WARRANTIES THAT THE SOFTWARE IS FREE OF DEFECTS,
#  MERCHANTABLE, FIT FOR A PARTICULAR PURPOSE, OR NON-INFRINGING.
#  See the License for the specific provisions governing your rights and
#  obligations concerning the Software.
#
#  The Initial Developer of the Original Code is: Sun Microsystems, Inc.
#
#  Copyright: 2001 by Sun Microsystems, Inc.
#
#  All Rights Reserved.
#
##########################################################################
#___INFO__MARK_END__

# define global variable in this namespace
global check_name 
global check_category 
global check_description 
global check_needs
global check_functions 
global check_errno 
global check_errstr 
global check_highest_level
global check_init_level_procedure
global check_root_access_needs
global env

#set check_root_access_needs "yes"


# define a level initialization procedure:
set check_init_level_procedure "tight_integration_init_level"

# define test's name and run level descriptions
set check_name            "tight_integration"
set check_category        "COMPATIBILITY SYSTEM VERIFIED"
set check_highest_level   1
set check_description(0)  "check tightly integrated parallel jobs submitted with qsub"
set check_description(1)  "check tightly integrated parallel jobs submitted with qrsh"

# define test's dependencies
set check_needs           "init_core_system" 


# define test's procedure order
set check_functions ""
lappend check_functions "tight_integration_setup"
lappend check_functions "tight_integration_function"
lappend check_functions "tight_integration_iz_578";# env vars NSLOTS and NHOSTS
lappend check_functions "tight_integration_master_killed"
lappend check_functions "tight_integration_slave_killed"
lappend check_functions "tight_integration_iz_575";# wallclock limit for tasks
lappend check_functions "tight_integration_massive"
lappend check_functions "tight_integration_cleanup"

# constants to be used by the tight integration test
global TIGHT_INTEGRATION_FUNCTIONAL_SLOTS 
global TIGHT_INTEGRATION_FUNCTIONAL_DURATION
global TIGHT_INTEGRATION_MASSIVE_SLOTS
global TIGHT_INTEGRATION_MASSIVE_DURATION
global TIGHT_INTEGRATION_ERROR_MESSAGES
global TIGHT_INTEGRATION_WARNING_MESSAGES


set TIGHT_INTEGRATION_FUNCTIONAL_SLOTS 5
set TIGHT_INTEGRATION_FUNCTIONAL_DURATION 10
set TIGHT_INTEGRATION_MASSIVE_SLOTS 200
set TIGHT_INTEGRATION_MASSIVE_DURATION 120

proc tight_integration_init_level {} {
   global ts_config
   global CHECK_ACT_LEVEL
   global submit_command TIGHT_INTEGRATION_FUNCTIONAL_SLOTS

   # for the functional test we want to schedule one task to each host
   # we set the allocation rule to round_robin and request as many slots
   # as we have exec hosts
   set num_hosts [llength $ts_config(execd_hosts)]
   set TIGHT_INTEGRATION_FUNCTIONAL_SLOTS $num_hosts

   switch -- $CHECK_ACT_LEVEL {
      "0" { 
         set submit_command qsub
         return 0    
      } 
      "1" {
         set submit_command qrsh
         return 0
      }
   } 

   return -1  ;# no other level 
}

# -------- local test procedures: initialization------------------------------

global submit_command

proc tight_integration_parse_execd_messages { mode } {
   global CHECK_PRODUCT_ROOT CHECK_CORE_EXECD TIGHT_INTEGRATION_MASSIVE_SLOTS
   global ts_config CHECK_OUTPUT CHECK_USER
   global TIGHT_INTEGRATION_ERROR_MESSAGES TIGHT_INTEGRATION_WARNING_MESSAGES

   foreach execd $ts_config(execd_hosts) {
      puts $CHECK_OUTPUT "check messages file from execd $execd ..."
      set TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode) 0
      set TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode) 0
      set messages [ get_execd_messages_file $execd ]
      get_file_content $execd $CHECK_USER $messages 
      if { $file_array(0) < 1 } {
         add_proc_error "tight_integration_setup" -1 "no execd(host=$execd) messages file:\n$messages"
      }
      for {set i 1 } { $i <= $file_array(0) } { incr i 1 } {
         if { [string match "*|E|*" $file_array($i)] } {
            incr TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode) 1
            set TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode,$TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode)) $file_array($i)
            puts $CHECK_OUTPUT $file_array($i)
         }
         if { [string match "*|W|*" $file_array($i)] } {
            incr TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode) 1
            set TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode,$TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode)) $file_array($i)
            puts $CHECK_OUTPUT $file_array($i)
         }
      }
   }

   foreach execd $ts_config(execd_hosts) {
      puts $CHECK_OUTPUT "error   messages for execd $execd: $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode)"
      puts $CHECK_OUTPUT "warning messages for execd $execd: $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode)"
   }

}


proc tight_integration_check_messages { mode1 mode2 } {
   global CHECK_PRODUCT_ROOT CHECK_CORE_EXECD TIGHT_INTEGRATION_MASSIVE_SLOTS
   global ts_config CHECK_OUTPUT CHECK_USER
   global TIGHT_INTEGRATION_ERROR_MESSAGES TIGHT_INTEGRATION_WARNING_MESSAGES

   set error_text ""
 
   foreach execd $ts_config(execd_hosts) {
      if { $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode1) != $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode2) } {
         puts $CHECK_OUTPUT "tight integration check produced execd error messages at $execd:"
         append error_text "tight integration check produced execd error messages at $execd:\n"
         for { set i [expr ( $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode1) + 1 ) ] } { $i <= $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode2) } { incr i 1 } { 
            puts $CHECK_OUTPUT $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode2,$i)
            set mstart [string last "|" $TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode2,$i)]
            incr mstart 1 
            append error_text [string range "$TIGHT_INTEGRATION_ERROR_MESSAGES($execd,$mode2,$i)\n" $mstart end]
         }
      }
      if { $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode1) != $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode2) } {
         puts $CHECK_OUTPUT "tight integration check produced execd waring messages at $execd:"
         append error_text "tight integration check produced execd waring messages at $execd:\n"

         for { set i [expr ( $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode1) + 1 ) ] } { $i <= $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode2) } { incr i 1 } {
             puts $CHECK_OUTPUT $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode2,$i)
             set mstart [string last "|" $TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode2,$i)]
             incr mstart 1 
             append error_text [string range "$TIGHT_INTEGRATION_WARNING_MESSAGES($execd,$mode2,$i)\n" $mstart end]
         }
      }
   }

   if { $error_text != "" } {
      add_proc_error "tight_integration_check_messages" -1 "$error_text"
   } else {
      puts $CHECK_OUTPUT "tight integration check produced no execd errors or warnings"
   }

}


proc tight_integration_setup  {} {
   global CHECK_PRODUCT_ROOT CHECK_CORE_EXECD TIGHT_INTEGRATION_MASSIVE_SLOTS
   global ts_config CHECK_OUTPUT CHECK_USER
   global TIGHT_INTEGRATION_ERROR_MESSAGES TIGHT_INTEGRATION_WARNING_MESSAGES

   # create parallel environment
   set pe(pe_name)            tight
   set pe(slots)              1000
   set pe(user_lists)         none
   set pe(xuser_lists)        none
   set pe(start_proc_args)    none
   set pe(stop_proc_args)     none
   set pe(allocation_rule)    "\$round_robin"
   set pe(control_slaves)     TRUE
   set pe(job_is_first_task)  FALSE
   add_pe pe

   # create a queue
   set queue_conf(load_thresholds) "np_load_avg=11"
   set queue_conf(slots) 10 
   add_queue tight.q "@allhosts" queue_conf

   # assign queue with pe
   assign_queues_with_pe_object tight.q "" tight

   tight_integration_parse_execd_messages startup

   set_error 0 "ok"
}

proc tight_integration_cleanup  {} {
   global CHECK_PRODUCT_TYPE CHECK_OUTPUT
   global TIGHT_INTEGRATION_ERROR_MESSAGES TIGHT_INTEGRATION_WARNING_MESSAGES

   get_config global_config
   puts $CHECK_OUTPUT "load_report_time is set to $global_config(load_report_time)"
   set report_time [transform_date_time $global_config(load_report_time)]
   set zero_time [transform_date_time "00:00:00"]
   set sleep_time [expr ( $report_time - $zero_time )]

   puts $CHECK_OUTPUT "sleeping for $sleep_time seconds to let each execd report its final state ..."
   sleep $sleep_time

   puts $CHECK_OUTPUT "additional sleep of 5 seconds ..."
   sleep 5

   tight_integration_parse_execd_messages cleanup

   tight_integration_check_messages startup cleanup
   
   unassign_queues_with_pe_object tight
   del_pe tight
   del_queue tight.q "" 0 1

   set_error 0 "ok"
}

# -------- local test procedures: utilities --------------------------
proc tight_integration_monitor {id started_var finished_var jobid_var info_var {iz_578 0}} {
   global CHECK_OUTPUT CHECK_HOST

   upvar $started_var  started
   upvar $finished_var finished
   upvar $jobid_var    jobid
   upvar $info_var     info

   set ret "unknown"

   set sp_id [lindex $id 1]
   set timeout 180

   
   expect_user {
      -i $sp_id timeout {
         add_proc_error tight_integration_monitor -1 "timeout waiting for tasks output"
         set ret "timeout"
      }
      -i $sp_id full_buffer {
         add_proc_error tight_integration_monitor -1 "buffer overflow please increment CHECK_EXPECT_MATCH_MAX_BUFFER value"
         set ret "error"
      }
      -i $sp_id eof {
         set ret "eof"
      }
      # workaround for a feature lacking in expect:
      # We have to parse complete lines.
      # expect_user ensures only that expect will parse input up to a newline,
      # but there seems to be no way to tell expect we want to examine each
      # individual line.
      -i $sp_id "?*\n" {
         #puts $CHECK_OUTPUT "entered default branch, data: $expect_out(0,string)"
         foreach line [ string trim [split $expect_out(0,string) "\n"]] {
            if {[string length [string trim $line]] > 0} {
               #puts $CHECK_OUTPUT "processing line: $line"
               switch -glob $line {
                  "petask ??? with pid ???????? started on host*" {
                     set task [lindex $line 1]
                     lappend info(tasks) $task
                     set info($task,pid) [lindex $line 4]
                     set info($task,host) [lindex $line 8]
                     incr started
                     puts $CHECK_OUTPUT "task $task started, total started: $started"
                     set ret "task started"
                  }
                  "petask ??? with pid ???????? finished*" {
                     set task [lindex $line 1]
                     incr finished
                     puts $CHECK_OUTPUT "task $task finished, total finished: $finished"
                     set ret "task finished"
                  }
                  "master task started with job id ?????? and pid*" {
                     set jobid [lindex $line 6]
                     lappend info(tasks) master
                     set info(master,pid) [lindex $line 9]
                     set info(master,host) $CHECK_HOST
                     puts $CHECK_OUTPUT "job $jobid started"
                     set ret "master started"
                  }
                  "master task submitted all sub tasks*" {
                     puts $CHECK_OUTPUT "master task submitted all tasks"
                     set ret "master submitted"
                  }
                  "master task exiting*" {
                     puts $CHECK_OUTPUT "job $jobid exited"
                     set ret "master finished"
                  }
                  "NSLOTS ??? NHOSTS ??? NQUEUES*" {
                     if {$iz_578} {
                        set nslots  [lindex $line 1]
                        set nhosts  [lindex $line 3]
                        set nqueues [lindex $line 5]
                        puts $CHECK_OUTPUT "nslots = $nslots, nhosts = $nhosts, nqueues = $nqueues"
                        if {$nslots == 0 || $nhosts == 0 || $nqueues == 0} {
                           add_proc_error tight_integration_monitor -1 "invalid environment setting for NSLOTS, NHOSTS, NQUEUES for pe task (Issue 578 present): $line"
                           set ret "error"
                           break
                        }
                     }
                     set ret "task running"
                  }

                    
                  "_start_mark_:(*)*" {
                     puts $CHECK_OUTPUT "got start mark from remote prog shell script"
                  }
                  "_exit_status_:(*)*" {
                     puts $CHECK_OUTPUT "got exit status from remote prog shell script"
                  }
                  "script done.*" {
                     puts $CHECK_OUTPUT "got \"script done.\" from remote prog shell script"
                  }
                  default {
                     add_proc_error tight_integration_monitor -1 "unexpected job output $line"
                     set ret "error"
                  }
               }
            }
         }
      }
   }

   return $ret
}

proc tight_integration_job_finished {job_state} {
   global CHECK_OUTPUT

   switch -exact $job_state {
      "unknown" -
      "timeout" -
      "eof" -
      "master finished" -
      "error" {
         set job_finished 1
      }
      
      "task started" -
      "task running" -
      "task finished" -
      "master started" -
      "master submitted" {
         set job_finished 0
      }

      default {
         set job_finished 1
      }
   }

   return $job_finished
}

proc tight_integration_wait_for_delete_state {jobid timeout} {
   global CHECK_OUTPUT

   set now [clock seconds]
   set end [expr $now + $timeout]
   while {$now < $end} {
      set state [lindex [get_job_state $jobid 1] 0]
      puts $CHECK_OUTPUT "job $jobid is in state $state"
      if {[string compare $state "dr"] == 0} {
         return 1
      } else {
         if {$state == -1} {
            puts $CHECK_OUTPUT "expected to see job $jobid in dr state, but job vanished. OK!"
            return 1
         }
      }
      set now [clock seconds]
      sleep 1
   }

   return 0
}

# -------- local test procedures: tests ------------------------------
proc tight_integration_function {} {
   global CHECK_OUTPUT CHECK_HOST
   global CHECK_TESTSUITE_ROOT CHECK_SCRIPT_FILE_DIR
   global TIGHT_INTEGRATION_FUNCTIONAL_SLOTS TIGHT_INTEGRATION_FUNCTIONAL_DURATION
   global submit_command
   puts $CHECK_OUTPUT "tight_integration: functional test"

   set masterq [get_queue_instance "tight.q" $CHECK_HOST]

   set id [submit_with_method $submit_command "-pe tight $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS -masterq $masterq -cwd -N tightf" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_job.sh" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_task.sh 1 $TIGHT_INTEGRATION_FUNCTIONAL_DURATION"]

   set started 0
   set finished 0
   set jobid 0
   set job_finished 0
   while {$job_finished == 0} {
      set job_state [tight_integration_monitor $id started finished jobid info]
      puts $CHECK_OUTPUT "job state: $job_state"
      set job_finished [tight_integration_job_finished $job_state]
   }

   if {$started != $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS} {
      add_proc_error tight_integration_functional -1 "only $started tasks out of $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS were started successfully"
   }

   if {$finished != $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS} {
      add_proc_error tight_integration_functional -1 "only $finished tasks out of $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS ran through successfully"
   }
   set sp_id [lindex $id 1]
   send -i $sp_id "\003" ;# send CTRL+C to stop evtl. running processes in that shell
   close_spawn_process $id

   wait_for_jobend $jobid tightf 60 0 1


   set_error 0 "ok"
}

# check if tasks are killed when they hit a wallclock limit
proc tight_integration_iz_575 {} {
   global ts_config
   global CHECK_OUTPUT CHECK_HOST CHECK_USER 
   global CHECK_CORE_EXECD CHECK_TESTSUITE_ROOT CHECK_SCRIPT_FILE_DIR
   global TIGHT_INTEGRATION_FUNCTIONAL_SLOTS TIGHT_INTEGRATION_FUNCTIONAL_DURATION
   global submit_command
   puts $CHECK_OUTPUT "tight_integration: iz 575 task wallclock limit"

   if {$ts_config(gridengine_version) == 53} {
      add_proc_error "tight_integration_iz_575" -3 "iz 575 does not run in 5.3 systems"
      set_error 0 "ok"
      return
   }

   # set wallclock limit on all slave queues
   if {[llength $CHECK_CORE_EXECD] < 2} {
      add_proc_error "tight_integration_iz_575" -3 "iz 575 test needs at least 2 execution hosts"
      set_error 0 "ok"
      return
   }

   set host_list {}
   foreach host $CHECK_CORE_EXECD {
      if {[string compare $host $CHECK_HOST] != 0} {
         lappend host_list $host
      }
   }

   set queue_conf(h_rt) "0:0:30"
   set_queue "tight.q" "$host_list" queue_conf

   set masterq [get_queue_instance "tight.q" $CHECK_HOST]

   # submit job
   set id [submit_with_method $submit_command "-pe tight $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS -masterq $masterq -cwd -N tightf" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_job.sh" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_task.sh 1 3600"]

   set started 0
   set finished 0
   set jobid 0
   set job_finished 0

   # wait until all tasks are started
   while {$job_finished == 0 && $started < $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS} {
      set job_state [tight_integration_monitor $id started finished jobid info]
      puts $CHECK_OUTPUT "job state: $job_state"
      set job_finished [tight_integration_job_finished $job_state]
   }

   # if startup was ok
   if {$job_finished == 0} {
      # job has to go into deleted state after rt limit - allow for 5s delay
      if {![tight_integration_wait_for_delete_state $jobid 35]} {
         add_proc_error "tight_integration_iz_575" -1 "slave tasks should have been killed due to runtime limit and set the job into dr state - issue 575 present"
         delete_job $jobid
      }
   }
   set sp_id [lindex $id 1]
   send -i $sp_id "\003" ;# send CTRL+C to stop evtl. running processes in that shell
   close_spawn_process $id

   wait_for_jobend $jobid tightf 60 0 1

   # reset queue runtime limit
   set queue_conf(h_rt) "INFINITY"
   set_queue "tight.q" "$host_list" queue_conf

   set_error 0 "ok"
}

# check NSLOTS, NHOSTS, NQUEUES for tasks
proc tight_integration_iz_578 {} {
   global CHECK_OUTPUT CHECK_HOST 
   global CHECK_TESTSUITE_ROOT CHECK_SCRIPT_FILE_DIR
   global TIGHT_INTEGRATION_FUNCTIONAL_SLOTS TIGHT_INTEGRATION_FUNCTIONAL_DURATION
   global submit_command
   puts $CHECK_OUTPUT "tight_integration: Issue 578 NSLOTS NHOSTS NQUEUES"

   set masterq [get_queue_instance "tight.q" $CHECK_HOST]

   set id [submit_with_method $submit_command "-pe tight $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS -masterq $masterq -cwd -N tightf" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_job.sh" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_task.sh 1 $TIGHT_INTEGRATION_FUNCTIONAL_DURATION"]


   set started 0
   set finished 0
   set jobid 0
   set job_finished 0
   while {$job_finished == 0} {
      set job_state [tight_integration_monitor $id started finished jobid info 1]
      puts $CHECK_OUTPUT "job state: $job_state"
      set job_finished [tight_integration_job_finished $job_state]
   }
   set sp_id [lindex $id 1]
   send -i $sp_id "\003" ;# send CTRL+C to stop evtl. running processes in that shell

   close_spawn_process $id

   wait_for_jobend $jobid tightf 60 0 1


   set_error 0 "ok"
}

# master task killed -> job has to be deleted
proc tight_integration_master_killed {} {
   global CHECK_OUTPUT CHECK_HOST CHECK_USER
   global CHECK_TESTSUITE_ROOT CHECK_SCRIPT_FILE_DIR
   global TIGHT_INTEGRATION_FUNCTIONAL_SLOTS TIGHT_INTEGRATION_FUNCTIONAL_DURATION
   global submit_command
   puts $CHECK_OUTPUT "tight_integration: master task killed - issue 579"

   set masterq [get_queue_instance "tight.q" $CHECK_HOST]

   set id [submit_with_method $submit_command "-pe tight $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS -masterq $masterq -cwd -N tightf" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_job.sh" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_task.sh 1 3600"]

   set started 0
   set finished 0
   set jobid 0
   set job_finished 0

   # wait until all tasks are started
   while {$job_finished == 0 && $started < $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS} {
      set job_state [tight_integration_monitor $id started finished jobid info]
      puts $CHECK_OUTPUT "job state: $job_state"
      set job_finished [tight_integration_job_finished $job_state]
   }

   # if startup was ok
   if {$job_finished == 0} {
      # wait for job to be running
      wait_for_job_state $jobid "r" 120

      # kill pid of master task and wait some seconds
      puts $CHECK_OUTPUT "killing process group of master task: pid $info(master,pid) on host $info(master,host)"
      start_remote_prog $info(master,host) $CHECK_USER kill "-9 -$info(master,pid)"

      if {![tight_integration_wait_for_delete_state $jobid 60]} {
         add_proc_error "tight_integration_master_killed" -1 "failed master task does not cause the job to be deleted (issue 579 present)"
         delete_job $jobid
      }
   }
   set sp_id [lindex $id 1]
   send -i $sp_id "\003" ;# send CTRL+C to stop evtl. running processes in that shell

   close_spawn_process $id

   wait_for_jobend $jobid tightf 60 0 1

   set_error 0 "ok"
}

# slave task killed -> job has to be deleted
proc tight_integration_slave_killed {} {
   global CHECK_OUTPUT CHECK_HOST CHECK_USER
   global CHECK_TESTSUITE_ROOT CHECK_SCRIPT_FILE_DIR
   global TIGHT_INTEGRATION_FUNCTIONAL_SLOTS TIGHT_INTEGRATION_FUNCTIONAL_DURATION
   global submit_command
   puts $CHECK_OUTPUT "tight_integration: slave task killed"

   set masterq [get_queue_instance "tight.q" $CHECK_HOST]

   set id [submit_with_method $submit_command "-pe tight $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS -masterq $masterq -cwd -N tightf" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_job.sh" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_task.sh 1 3600"]

   set started 0
   set finished 0
   set jobid 0
   set job_finished 0

   # wait until all tasks are started
   while {$job_finished == 0 && $started < $TIGHT_INTEGRATION_FUNCTIONAL_SLOTS} {
      set job_state [tight_integration_monitor $id started finished jobid info]
      puts $CHECK_OUTPUT "job state: $job_state"
      set job_finished [tight_integration_job_finished $job_state]
   }

   # if startup was ok
   if {$job_finished == 0} {
      # wait for job to be running
      wait_for_job_state $jobid "r" 120

      # kill pid of master task and wait some seconds
      # we take task 1: if we have more than one host, it will be on a slave host
      puts $CHECK_OUTPUT "killing slave task: process group $info(1,pid) on host $info(1,host)"
      start_remote_prog $info(1,host) $CHECK_USER kill "-9 -$info(1,pid)"

      if {![tight_integration_wait_for_delete_state $jobid 60]} {
         add_proc_error "tight_integration_slave_killed" -1 "failed slave task does not cause the job to be deleted"
         delete_job $jobid
      }
   }
   set sp_id [lindex $id 1]
   send -i $sp_id "\003" ;# send CTRL+C to stop evtl. running processes in that shell

   close_spawn_process $id

   wait_for_jobend $jobid tightf 60 0 1

   set_error 0 "ok"
}

proc tight_integration_massive {} {
   global CHECK_OUTPUT CHECK_CORE_EXECD CHECK_HOST
   global TIGHT_INTEGRATION_MASSIVE_SLOTS TIGHT_INTEGRATION_MASSIVE_DURATION
   global CHECK_TESTSUITE_ROOT CHECK_SCRIPT_FILE_DIR
   global submit_command
   puts $CHECK_OUTPUT "tight_integration: massive parallel test"

   # count the number of slots in the cluster to check if tests can be run
   set total_slots 0
   foreach host $CHECK_CORE_EXECD {
      get_queue [get_queue_instance "tight.q" $host] queue_conf
      incr total_slots $queue_conf(slots)
      unset queue_conf
   }

   # we use a masterq on the check-host as we tail the output file here
   set masterq [get_queue_instance "tight.q" $CHECK_HOST]

   # increase the number of slots util maximum is reached
   for {set massive_slots 19} {$massive_slots <= $TIGHT_INTEGRATION_MASSIVE_SLOTS} {incr massive_slots 10} {
      puts $CHECK_OUTPUT "running parallel job with $massive_slots slots"
      # check if test can run at all in this cluster
      if {$total_slots < [expr $massive_slots + 1]} {
         add_proc_error tight_integration_massive -3 "test cannot run massive parallel job with $massive_slots tasks + the master task - cluster only has $total_slots slots"
         set_error 0 "ok"
         return
      }

      set id [submit_with_method $submit_command "-pe tight $massive_slots -masterq $masterq -cwd -N tightm" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_job.sh" "$CHECK_TESTSUITE_ROOT/$CHECK_SCRIPT_FILE_DIR/pe_task.sh 1 $TIGHT_INTEGRATION_MASSIVE_DURATION"]

      set started 0
      set finished 0
      set jobid 0
      set job_finished 0
      
      while {$job_finished == 0} {
         set job_state [tight_integration_monitor $id started finished jobid info]
         set job_finished [tight_integration_job_finished $job_state]
      }

      if {$started != $massive_slots} {
         add_proc_error tight_integration_massive -1 "only $started tasks out of $massive_slots were started successfully"
      }

      if {$finished != $massive_slots} {
         add_proc_error tight_integration_massive -1 "only $finished tasks out of $massive_slots ran through successfully"
      }
      set sp_id [lindex $id 1]
      send -i $sp_id "\003" ;# send CTRL+C to stop evtl. running processes in that shell

      close_spawn_process $id

      wait_for_jobend $jobid tightm 300 0 1
   }

   set_error 0 "ok"
}
